apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

// Load local properties with API keys
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdk 36
    buildToolsVersion '36.0.0'
    defaultConfig {
        applicationId 'com.b_lam.resplash'
        minSdkVersion 21
        targetSdk 36
        versionCode 62
        versionName '3.4.2'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField("String", "RELEASE_APP_ID", localProperties['releaseAppId'])
        buildConfigField("String", "RELEASE_SECRET", localProperties['releaseSecret'])
        buildConfigField("String", "DEV_APP_ID", localProperties['devAppId'])
        buildConfigField("String", "DEV_SECRET", localProperties['devSecret'])
        buildConfigField("String", "GOOGLE_PLAY_LICENSE_KEY", localProperties['googlePlayLicenseKey'])
        renderscriptTargetApi 33 as int
        renderscriptSupportModeEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
    }
    buildFeatures {
        viewBinding true
    }
    bundle {
        language {
            enableSplit = false
        }
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    lint {
        disable 'MissingTranslation'
    }
    namespace 'com.b_lam.resplash'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support libraries
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.3.6'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.10.0'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-service:2.8.7'

    // Room
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    kapt 'androidx.room:room-compiler:2.6.1'

    // Material components
    implementation 'com.google.android.material:material:1.12.0'

    // Billing
    implementation 'com.android.billingclient:billing-ktx:7.1.1'

    // Kotlin coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1'

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.14'

    // Okio
    implementation 'com.squareup.okio:okio:3.10.2'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.11.0'

    // Moshi
    implementation 'com.squareup.moshi:moshi:1.15.2'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.15.2'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    kapt 'com.github.bumptech.glide:compiler:4.13.2'

    // Koin
    implementation 'io.insert-koin:koin-android:4.0.4'
    implementation 'io.insert-koin:koin-androidx-workmanager:4.0.4'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:33.12.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:22.4.0'
    implementation 'com.google.firebase:firebase-crashlytics:19.4.2'
    implementation('com.google.firebase:firebase-inappmessaging-ktx:20.1.3', {
        exclude group: 'io.grpc', module: 'grpc-okhttp'
    })
    // Workaround for https://github.com/firebase/firebase-android-sdk/issues/2642
    implementation 'io.grpc:grpc-okhttp:1.71.0'
    implementation 'com.google.firebase:firebase-firestore-ktx:25.1.3'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    // Play Core
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // View binding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate:1.5.9'
    
    // Lottie
    implementation 'com.airbnb.android:lottie:6.6.4'

    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Pager indicator
    implementation 'com.github.intik:overflow-pager-indicator:3.1.0'

    // PrettyTime
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.4.Final'

    // Open source notices
    implementation 'com.google.android.gms:play-services-oss-licenses:17.1.0'

    // Muzei
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.2'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    // Unit tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'

    // Room testing
    androidTestImplementation "androidx.room:room-testing:2.6.1"
}
